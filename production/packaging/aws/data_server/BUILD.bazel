# Copyright 2023 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load(
    "@rules_pkg//pkg:mappings.bzl",
    "pkg_attributes",
    "pkg_files",
    "pkg_mklink",
)
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_pkg//pkg:zip.bzl", "pkg_zip")

pkg_files(
    name = "server_executables",
    srcs = [
        "//components/data_server/server",
        "//components/tools:get_region",
        "//production/packaging/aws/data_server/bin:init_server_basic",
    ],
    attributes = pkg_attributes(mode = "0555"),
    prefix = "/",
)

# Create a symlink between where kmstool_enclave_cli expects shell to be
# (/bin/sh) and where it actually is on our image (/busybox/sh).
pkg_mklink(
    name = "busybox_sh_symlink",
    link_name = "/bin/sh",
    target = "/busybox/sh",
)

server_binaries = [
    ":server_executables",
    ":busybox_sh_symlink",
]

pkg_zip(
    name = "server_binaries",
    srcs = server_binaries,
)

pkg_tar(
    name = "server_binaries_tar",
    srcs = server_binaries,
)

# This image target is meant for testing running the server in an enclave using.
#
# See project README.md on how to run the image.
oci_image(
    name = "server_docker_image",
    base = select({
        "@platforms//cpu:arm64": "@runtime-debian-debug-nonroot-arm64//image",
        "@platforms//cpu:x86_64": "@runtime-debian-debug-nonroot-amd64//image",
    }),
    entrypoint = [
        "/init_server_basic",
        "--with_proxify",
        # Remaining flags are forwarded to `/server`.
        "--",
        # Note: these ports must correspond with those specified in envoy.yaml.
        "--port=50051",
        # These affect PCR0, so changing these would result in the loss of ability to communicate with
        # the downstream components
        "--public_key_endpoint=https://publickeyservice.pa-3.aws.privacysandboxservices.com/v1alpha/publicKeys",
        "--stderrthreshold=0",
    ],
    tars = [
        "@google_privacysandbox_servers_common//src/aws/proxy:libnsm_and_proxify_tar",
        "@google_privacysandbox_servers_common//src/cpio/client_providers/kms_client_provider/aws:kms_binaries",
        "//production/packaging/aws/resolv:resolv_config_layer",
        ":server_binaries_tar",
    ],
)

oci_tarball(
    name = "server_docker_tarball",
    image = ":server_docker_image",
    repo_tags = ["bazel/production/packaging/aws/data_server:server_docker_image"],
)

container_structure_test(
    name = "structure_test",
    size = "medium",
    configs = ["test/structure.yaml"],
    driver = "tar",
    image = ":server_docker_tarball",
)

container_structure_test(
    name = "commands_test",
    size = "small",
    configs = ["test/commands.yaml"],
    driver = "docker",
    image = ":server_docker_image",
)

# server artifacts
pkg_zip(
    name = "server_artifacts",
    srcs = server_binaries,
)

genrule(
    name = "copy_to_dist",
    srcs = [
        ":server_artifacts",
        ":server_docker_tarball",
        "//public/query:query_api_descriptor_set",
    ],
    outs = ["copy_to_dist.bin"],
    cmd_bash = """cat << EOF > '$@'
mkdir -p dist/debian
cp $(execpath :server_artifacts) dist/debian
cp $(execpath :server_docker_tarball) dist/server_docker_image.tar
cp $(execpath //public/query:query_api_descriptor_set) dist
# retain previous server_docker_image.tar location as a symlink
ln -rsf dist/server_docker_image.tar dist/debian/server_docker_image.tar
builders/tools/normalize-dist
EOF""",
    executable = True,
    local = True,
    message = "copying server artifacts to dist directory",
)
